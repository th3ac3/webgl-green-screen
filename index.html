<html>
<head>
	<title>Robert Allen: WebGL</title>
	<meta charset="UTF-8">

	<!-- jQuery -->
	<script src="js/jquery-3.3.1.min.js" type="text/javascript"></script>
	
	<!-- glMatrix class -->
	<script src="js/glMatrix-0.9.5.min.js" type="text/javascript"></script>

	<!-- Our WebGL code -->
	<script src="js/cube.js" type="text/javascript"></script>
	<script src="js/pyramid.js" type="text/javascript"></script>
	<script src="js/sphere.js" type="text/javascript"></script>
	<script src="js/screen.js" type="text/javascript"></script>

	<!-- Vertex shader -->
	<script id="shader-vs" type="x-shader/x-vertex"> 
		precision highp float;
		
		attribute vec3 XYZ;
		attribute vec3 RGB;
		attribute vec2 TEX;
		attribute vec3 NORM;
		
		// Transformation Matrixes
		uniform mat4 PMatrix;
		uniform mat4 MVMatrix;
		uniform mat4 VMatrix;
		uniform mat3 NMatrix;

		// Light Properties
		uniform vec4 LightPos;
		uniform vec3 Ambient;
		uniform vec3 Diffuse;
		uniform vec3 Specular;
		
		varying vec4 col;
		varying vec2 TexCoord;

		vec4 phong() {
			//  P is the vertex coordinate on body
			vec3 P = vec3(MVMatrix * vec4(XYZ,1.0));
			//  N is the object normal at P
			vec3 N = normalize(NMatrix * NORM);
			//  Light Position 
			vec3 L = normalize(vec3(VMatrix * LightPos) - P);

			//  Ambient (ignoring emission since non-existant on cube)
			vec3 color = Ambient;

			//  Diffuse light intensity is cosine of light and normal vectors
			float Id = dot(L,N);
			if (Id>0.0)
			{
				//  Add diffuse
				color += Id * Diffuse;
				//  R is the reflected light vector R = 2(L.N)N - L
				vec3 R = reflect(-L, N);
				//  V is the view vector (eye at the origin)
				vec3 V = normalize(-P);
				//  Specular is cosine of reflected and view vectors
				float Is = dot(R,V);
				// Set default shininess val to 16
				if (Is>0.0) color += pow(Is, 16.0) * Specular;
			}

			//  Return sum of color components
			return vec4(RGB * color, 1.0);
		}
		
		void main(void){
			col = phong();
			TexCoord = TEX;
			gl_Position = PMatrix * MVMatrix * vec4(XYZ,1);
		}
	</script> 
	 
	<!-- Normal Fragment shader -->
	<script id="shader-fs" type="x-shader/x-fragment"> 
		precision highp float;

		varying vec4 col;
		varying vec2 TexCoord;

		uniform sampler2D tex1;
		uniform float noTexture;

		// If noTexture set to 1 then don't show the texture and don't do lighting
		vec4 noTex(vec4 c) {
			vec4 white = vec4(1.0);
			vec4 black = vec4(0.0, 0.0, 0.0, 1.0);
			vec4 noTex = vec4(noTexture);

			c = clamp(c + noTex, black, white);
			vec4 color = clamp(col + noTex, black, white);

			return c * color;
		}

		void main(void){
			vec4 c = texture2D(tex1, TexCoord);
			gl_FragColor = noTex(c);
		}
	</script> 

	<!-- Fragment Green Screen Shader -->
	<!-- https://groups.csail.mit.edu/graphics/classes/CompPhoto06/html/lecturenotes/07_matting_6.pdf -->
	<script id="shader-green-screen" type="x-shader/x-fragment"> 
		precision highp float;

		float a1 = 2.25; // threshold
		float a2 = 1.0; // usually 0.5 < a2 < 1.5

		varying vec4 col;
		varying vec2 TexCoord;

		uniform sampler2D tex1;
		uniform sampler2D tex2;
		uniform float noTexture;

		// Vlahos assumption
		// Returns 1 if r or b are dominant
		// Returns [0, 1) if g is dominant
		float getAlpha(vec4 c) {
			float alpha = 1.0 - a1 * (c.g - a2 * max(c.r, c.b));
			return clamp(alpha, 0.0, 1.0);
		}

		// Responsible for removing green spill from reflections
		vec4 despill(vec4 c) {
			// If g > avg(r,b) set g = avg(r,b)
			// else do nothing
			c.g -= max(c.g - mix(c.b, c.r, 0.5), 0.0);

			return c;
		}

		// If noTexture set to 1 then don't show the texture and don't do lighting
		// Used to show light ball
		vec4 noTex(vec4 c) {
			vec4 white = vec4(1.0);
			vec4 black = vec4(0.0, 0.0, 0.0, 1.0);
			vec4 noTex = vec4(noTexture);

			c = clamp(c + noTex, black, white);
			vec4 color = clamp(col + noTex, black, white);

			return c * color;
		}

		void main(void) {
			vec4 fg = texture2D(tex1, TexCoord);
			vec4 bg = texture2D(tex2, TexCoord);

			float a = getAlpha(fg);
			fg = despill(fg);

			vec4 c = mix(bg, fg, a);

			gl_FragColor = noTex(c);
		}
	</script> 

	<!-- Fragment Ripple Shader -->
	<script id="shader-ripple" type="x-shader/x-fragment">
		precision highp float;

		varying vec4 col;
		varying vec2 TexCoord;

		uniform sampler2D tex1;
		uniform float noTexture;
		// Controls how much a ripple distorts the texture
		uniform float rippleDistortion;
		// Controls how many ripples we get
		uniform float rippleAmount;
		// Ripple speed
		uniform float rippleSpeed;
		uniform float time;

		vec2 ripple(vec2 tc) {
			// convert tc to value between [-1,1]
			vec2 p = 2.0 * tc - 1.0;
			// Calculate distance from center of texture
			float len = length(p);
			// Alter texture coordinates by cos
			vec2 uv = tc + cos(len * rippleAmount - rippleSpeed * time) * rippleDistortion;

			return uv;
		}

		// If noTexture set to 1 then don't show the texture and don't do lighting
		vec4 noTex(vec4 c) {
			vec4 white = vec4(1.0);
			vec4 black = vec4(0.0, 0.0, 0.0, 1.0);
			vec4 noTex = vec4(noTexture);

			c = clamp(c + noTex, black, white);
			vec4 color = clamp(col + noTex, black, white);

			return c * color;
		}

		void main(void){
			vec2 uv = ripple(TexCoord);
			vec4 c = texture2D(tex1, uv);
			gl_FragColor = noTex(c);
		}
	</script>

	<script src="js/main.js" type="text/javascript"></script>
	
	<!-- Page styles -->
	<link rel="stylesheet" type="text/css" href="style/style.css">
</head>
<body onload="webGLStart();" onresize="canvas.resize();"> 
	<div class="center wrapper">
		<canvas id="canvas" width="500" height="500"></canvas>
	</div>
	<div class="wrapper">
		<label for="shader-program">Shader: </label>
		<select id="shader-program" name="shader-program">
			<option value="0" selected="selected">Normal</option>
			<option value="1">Green Screen</option>
			<option value="2">Ripple</option>
		</select>
		<br>
		<label for="texture-fg">Texture Foreground: </label>
		<select id="texture-fg" name="texture-fg">
			<option value="0" selected="selected">Crate</option>
			<option value="1">Firefox Video</option>
			<option value="2">Green Screen Video</option>
			<option value="3">Yosemite Video</option>
		</select>
		<br>
		<label for="texture-bg">Texture Background: </label>
		<select id="texture-bg" name="texture-bg">
			<option value="0" selected="selected">Crate</option>
			<option value="1">Firefox Video</option>
			<option value="2">Green Screen Video</option>
			<option value="3">Yosemite Video</option>
		</select>
		<div>
			<div class="half">
				<h3>Controls</h3>
				<p>Click and Drag: Rotate object around</p>
				<p>Next Item: Right Arrow</p>
				<p>Previous Item: Left Arrow</p>
				<p>Pause Video: Space (Only for video textures)</p>
				<p>l: Toggle lighting</p>
				<p>p: Toggle projection</p>
				<p>m: Toggle "movie mode" (easy view mode)</p>
			</div>
			<div class="half">
				<h3>Ripple Shader Controls</h3>
				<p>q: Increase amount of ripples</p>
				<p>a: Decrease amount of ripples</p>
				<p>w: Increase distortion of ripples</p>
				<p>s: Decrease distortion of ripples</p>
				<p>e: Increase ripple speed</p>
				<p>d: Decrease ripple speed</p>
			</div>
		</div>
	</div>
</body>
</html>
